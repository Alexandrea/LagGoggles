buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
boolean isSponge = hasProperty("isSponge")
boolean isClient = hasProperty("isClient")
String buildType = (isSponge ? "SPONGEFORGE" : "FORGE")
println("Compiling: " + buildType)

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the nessasary things for Forge to be setup.

repositories {
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven/"
    }
}

if(isClient == false) {
    delete fileTree('run/mods') {
        include 'LagGoggles*'
    }
}

version = "1.12.2-4.2-HOTFIX"
group= "cf.terminator.laggoggles" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LagGoggles-" + buildType

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"


    options.fork = true
    options.forkOptions.executable = 'javac'
    options.compilerArgs << "-XDignore.symbol.file"
}

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"

    replaceIn "Main.java"
    replace '${version}', buildType + "-" + project.version
    mappings = "snapshot_20180808"
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    compile 'org.spongepowered:spongeforge:1.12.2-2768-7.1.6-RC3643:dev'



    if(isSponge) {
        compileOnly ('org.spongepowered:mixin:0.7.11-SNAPSHOT'){
            ['gson','guava','commons-io'].each {
                exclude module: it
            }
        }
    }else{
        embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
            ['gson','guava','commons-io'].each {
                exclude module: it
            }
            exclude module: 'launchwrapper'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "WORLDSERVER", isSponge ? 'MixinWorldServerSponge' : 'MixinWorldServer'

    // replace stuff
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'mixins.laggoggles.json'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'WORLDSERVER':(isSponge ? 'MixinWorldServerSponge' : 'MixinWorldServer')
    }

    // copy everything else, thats not the mcmod.info or mixins.laggoggles.json
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'mixins.laggoggles.json'
    }

}

sourceSets {
    main {
        refMap = "mixins.laggoggles.refmap.json"
    }
}

jar {
    from configurations.embed.collect {
        if(it.isDirectory()){
            return it
        }else{
            FileTree tmp = zipTree(it)
            for(File s : tmp.files) {
                if (s.toString().endsWith(".html")){
                    System.out.println("File " + s + " removed")
                    tmp.files.remove(s)
                }
            }
            return tmp
        }

    }
    rename 'LICENSE.txt', 'LICENSE-mixin.txt'
    from ('LICENSE.md')
    manifest.attributes(
            'FMLCorePluginContainsFMLMod': 'true',
            'MixinConfigs': 'mixins.laggoggles.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': '0',
            'ForceLoadAsMod': 'true'
    )
}

task finalize << {
    if(isClient == false) {
        println("Copied " + jar.archivePath + " into run/mods")
        copy {
            from jar.archivePath
            into "run/mods"
        }
    }
}

build.finalizedBy(finalize)